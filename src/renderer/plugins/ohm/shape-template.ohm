ShapeTemplateGrammar {
  ShapeTemplate = ShapeElement | PathTemplate
  PathTemplate = PathFragment (Expr PathFragment)*
  PathFragment = (PathCommand|digit|"."|","|"-")*
  PathCommand = "M"|"m"|"L"|"l"|"H"|"h"|"V"|"v"|"Z"|"z"|"C"|"c"|"S"|"s"|"Q"|"q"|"T"|"t"|"A"|"a"
  Expr = "${" ExprBody "}"
  ExprBody = ExprBody "+" path -- union
  		     | ExprBody "-" path -- difference
           | ExprBody "&" path -- intersection
           | ExprBody ">" path -- join
           | path


  ShapeElement = "<" ShapeTag ShapeAttributes "/>"
  ShapeTag = "circle" | "ellipse" | "polygon" | "rect"
  ShapeAttributes = (~"/>" any)*  // nice to have validate also attributes validity

  path = literal | var
  literal = "(" (~")" any)* ")"
  var = path_ref "|" transform -- transform
      | path_ref
  path_ref = (letter | digit | "." | "_" | "/" | "#" )* ("@" "0".."3")?
  transform = t_reverse | t_translate | t_rotate

  t_reverse = "reverse"
  t_translate = "translate(" number "," space* number ")"
  t_rotate = "rotate(" ("0" | "90" | "180" | "270") ")"

  number = digit* "." digit+  -- fract
         | digit+             -- whole
}
